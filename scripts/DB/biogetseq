#!/usr/bin/perl
# $Id: biogetseq,v 1.4 2003-02-20 22:20:56 heikki Exp $
#
# OBDA Registry compliant sequence retrieval script
#
# Copyright Heikki Lehvaslaiho <heikki@ebi.ac.uk>
# You may distribute this program under the same terms as perl itself


use Bio::DB::Registry;
use Bio::SeqIO;
use Getopt::Long;
use strict;

my ($help, $format, $namespace, $dbname) = ('', 'embl', 'acc', 'embl');
GetOptions ("help" => \$help, "format=s" => \$format,
            "namespace=s" => \$namespace, "dbname=s" => \$dbname );
usage() if $help;
usage() unless @ARGV;


my $get_function = 'get_Seq_by_'. $namespace;

my $registry = new Bio::DB::Registry();
while (my $id = shift) {
    my $db = $registry->get_database($dbname);
    my $seq = $db->$get_function($id);

    if ($seq) {
        my $out = new Bio::SeqIO('-format' => $format);
        $out->write_seq($seq);
    } else {
        print STDERR "Could not find sequence with identifier [$id]";
    }
}


sub usage {
    print "Usage: biogetseq --dbname embl --format embl --namespace acc  id [ id ... ]*\n";
    print "       dbname defaults to embl\n";
    print "       format defaults to embl\n";
    print "       namespace defaults to 'acc' ['id', 'acc', 'version']\n";
    print "       rest of the arguments is  a list of ids in the given namespace\n";
    exit;
}
